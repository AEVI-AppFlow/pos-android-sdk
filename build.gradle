buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven {
            name = "github-aevi-appflow"
            url = uri("https://maven.pkg.github.com/AEVI-AppFlow/gradle-scripts")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: gh_username
                password = System.getenv("GITHUB_TOKEN") ?: gh_token
            }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8'
        classpath 'com.jakewharton:butterknife-gradle-plugin:10.2.3'
        classpath 'com.aevi.sdk.build:gradle-scripts:1.5.2'
        classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
    }
}

allprojects {
    ext.applicationTargetSdkVersion = 32
    ext.applicationMinSdkVersion = 24
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
    }
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'

ext.gradleScript = { path ->
    return rootProject.buildscript.classLoader.getResource(path).toURI()
}

apply from: gradleScript('root/common-tasks.gradle')
apply from: gradleScript('publish/maven-central-repo.gradle')


task cleanDocs(type: Delete) {
    delete 'documentation/public'
}

task copyApiLibs(type: Copy) {
    from 'payment-initiation-api/build/libs'
    from 'payment-flow-service-api/build/libs'
    into 'artifacts/libs'
}

apply from: 'dependencies.gradle'

def exportedProjects = [
        ":flow-base-api",
        ":payment-flow-service-api",
        ":payment-initiation-api"
]

def baseAndFlowService = [
        ":flow-base-api",
        ":payment-initiation-api",
        ":payment-flow-service-api"
]

def baseAndInitiation = [
        ":flow-base-api",
        ":payment-initiation-api"
]

task mergeBaseAndFlowServiceDocs(type: Javadoc, dependsOn: 'flow-base-api:androidJavadocs') {

    source += baseAndFlowService.collect {
        evaluationDependsOn(it)
        project(it).tasks['androidJavadocs'].source
    }

    classpath += files(baseAndFlowService.collect {
        evaluationDependsOn(it)
        project(it).tasks['androidJavadocs'].classpath
    })

    destinationDir = file('documentation/public/javadocs/payment-flow-service-api')
    title = "Payment Flow Service API"
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

task mergeBaseAndInitiationDocs(type: Javadoc) {

    source += baseAndInitiation.collect {
        evaluationDependsOn(it)
        project(it).tasks['androidJavadocs'].source
    }

    classpath += files(baseAndInitiation.collect {
        evaluationDependsOn(it)
        project(it).tasks['androidJavadocs'].classpath
    })

    destinationDir = file('documentation/public/javadocs/payment-initiation-api')
    title = "Payment Initiation API"
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

task javadocs(dependsOn: [mergeBaseAndFlowServiceDocs, mergeBaseAndInitiationDocs]) {
}

task createJavadocsArtifact(dependsOn: [javadocs], type: Zip) {
    from 'documentation/public/'
    archiveName 'javadocs.zip'
    destinationDirectory = file('artifacts/docs')
}

apply from: 'dist.gradle'
