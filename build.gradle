version = "1.0.0-SNAPSHOT"

buildscript {
    repositories {
        jcenter()
        google()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1'
    }
}

allprojects {

    project.ext {
        apiVersion = rootProject.version
        sampleVersionCode = 1
    }

    repositories {
        mavenLocal()
        maven { url 'https://maven.google.com' }
        jcenter()
        mavenCentral()
        maven {
            url "s3://sdk-snapshots.aevi.com/maven2"
            credentials(AwsCredentials) {
                accessKey aws_accessid
                secretKey aws_accesskey
            }
        }
        maven {
            url "s3://sdk-qa.aevi.com/maven2"
            credentials(AwsCredentials) {
                accessKey aws_accessid
                secretKey aws_accesskey
            }
        }
        maven {
            url "s3://sdk-releases.aevi.com/maven2"
            credentials(AwsCredentials) {
                accessKey aws_accessid
                secretKey aws_accesskey
            }
        }
        maven {
            url "http://dl.bintray.com/aevi/aevi-uk"
        }
        maven {
            url "http://sdk-public-artifacts.s3-website-eu-west-1.amazonaws.com/maven2"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
    delete 'documentation/public'
}

task copyApiLibs(type: Copy) {
    from 'payment-initiation-api/build/libs'
    from 'payment-service-api/build/libs'
    from 'flow-service-api/build/libs'
    into 'artifacts/libs'
}

task copyApks(type: Copy) {
    from 'PaymentInitiationSample/build/outputs/apk/debug'
    from 'PaymentServiceSample/build/outputs/apk/debug'
    from 'FlowServiceSample/build/outputs/apk/debug'
    exclude 'output.json'
    into 'artifacts/samples'
}

apply from: 'dependencies.gradle'

def exportedProjects = [
        ":api-constants",
        ":flow-base-api",
        ":flow-service-api",
        ":payment-initiation-api",
        ":payment-service-api"
]

configure(subprojects.findAll {
    exportedProjects.contains(":" + it.name)
}) {
    apply plugin: 'java'
    apply plugin: 'de.fuerstenau.buildconfig'
    apply plugin: 'idea'
}

task alljavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file('documentation/public/javadocs/merged')
}

apply from: 'dist.gradle'