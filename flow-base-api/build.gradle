apply plugin: 'java-library'

sourceCompatibility = "11"
targetCompatibility = "11"

apply from: gradleScript('lib/versioning.gradle')
apply from: gradleScript('publish/publishing-utils.gradle')

group = 'com.aevi.sdk.flow'
version = libraryVersion
description = 'flow-base-api'

publishingUtils?.publication {
    groupId group
    artifactId project.name
    version libraryVersion
}

apply from: gradleScript('publish/publish-maven-central.gradle')

dependencies {
    implementation files('libs/android-mockable-default-values-26.jar')
    implementation apiDependencies.gson
    implementation apiDependencies.rxJava
    implementation apiDependencies.rxMessenger
    implementation apiDependencies.jsonConverter
    implementation apiDependencies.archLifecycle
    implementation apiDependencies.archLifecycleJava8
    implementation apiDependencies.configApi

    testImplementation project(':payment-flow-service-api')
    testImplementation files('libs/android-mockable-default-values-26.jar')
    testImplementation testDependencies.junit
    testImplementation testDependencies.hamcrestAll
    testImplementation testDependencies.mockito
    testImplementation testDependencies.assertJ
    testImplementation testDependencies.robolectric
}

javadoc {
    source = sourceSets.main.allJava
    destinationDir = file("../documentation/public/javadocs/flow-api")
    classpath = files(sourceSets.main.compileClasspath, sourceSets.main.runtimeClasspath)
    exclude "**/**ClientImpl**"
    exclude "**/util/**"
    exclude "**/service/**"
    exclude "**/ApiBase**"
    exclude "**/AppMessage**"
    exclude "**/AppMessageTypes**"
    exclude "**/BaseModel**"
    exclude "**/InternalData**"
    exclude "**/NoOpModel**"
    exclude "**/MessageErrors**"
    title = "Flow API"
    options.memberLevel = JavadocMemberLevel.PUBLIC
    failOnError = false
}

buildConfig {
    buildConfigField("String", "NAME", "\"${project.name}\"")
    buildConfigField("String", "VERSION", provider { "\"${project.version}\"" })
    buildConfigField("long", "BUILD_TIME", "${System.currentTimeMillis()}L")

    packageName = "com.aevi.sdk.flow"
    className = "FlowBaseConfig"
}


